group 'net.ncguy.argent'
version '0.1'

apply plugin: 'java'

sourceCompatibility = 1.8

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    }
    dependencies {
    }
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
}

configurations {
    provided
}

sourceSets {
    main.compileClasspath += configurations.provided
    test.compileClasspath += configurations.provided
    test.runtimeClasspath += configurations.provided
}

task dist(type: Jar) {
    manifest {
        attributes 'Implementation-Title': 'Gradle Jar File Example',
                'Implementation-Version': version
    }
    baseName = project.name + '-dist'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

task fatJar(type: Jar) {
    manifest {
        attributes 'Implementation-Title': 'Gradle Jar File Example',
                'Implementation-Version': version
    }
    baseName = project.name + '-fat'
    from { configurations.provided.collect { it.isDirectory() ? it : zipTree(it) } }
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

task testFatJar(type: Jar) {
    manifest {
        attributes 'Implementation-Title': 'Gradle Jar File Example',
                'Implementation-Version': version,
                "Main-Class": 'TestLauncher'
    }
    baseName = project.name + '-test-fat'
    from { configurations.provided.collect { it.isDirectory() ? it : zipTree(it) } }
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    from { sourceSets.test.output }
    with jar
}

ext {
    appName = "ArgentEngine"
    gdxVersion = '1.9.3'
    ashleyVersion = '1.7.0'
}

dependencies {

    provided "com.badlogicgames.gdx:gdx:$gdxVersion"
    provided "com.badlogicgames.gdx:gdx-bullet:$gdxVersion"
    provided "com.badlogicgames.ashley:ashley:$ashleyVersion"

    provided "com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdxVersion"
    provided "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
    provided "com.badlogicgames.gdx:gdx-bullet-platform:$gdxVersion:natives-desktop"

    provided "com.kotcrab.vis:vis-ui:1.1.1"

    compile("org.lwjgl.lwjgl:lwjgl_util:2.9.3") {
        exclude group: "org.lwjgl.lwjgl", module: "lwjgl"
    }

    compile "com.google.code.gson:gson:2.6.2"
    compile "org.reflections:reflections:0.9.10"
    compile "com.esotericsoftware:kryonet:2.22.0-RC1"
    compile "org.saintandreas:jovr:1.3.0.0"

    compile "com.fifesoft:rsyntaxtextarea:2.5.8"

    compile("com.strongjoshua:libgdx-inGameConsole:0.4.0") {
        exclude group: 'com.badlogicgames.gdx', module: 'gdx'
    }

    compile fileTree('libs')

    testCompile group: 'junit', name: 'junit', version: '4.11'
}
