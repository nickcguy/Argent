#version 450

uniform vec3 eye;
uniform vec3 ray00;
uniform vec3 ray01;
uniform vec3 ray10;
uniform vec3 ray11;

uniform vec2 u_screenRes;
in VS_OUT {
    vec3 Normal;
    vec2 TexCoords;
    float Depth;
    vec4 Position;
} vsOut;

out vec4 Colour;

struct box {
  vec3 min;
  vec3 max;
};

#define MAX_SCENE_BOUNDS 100.0
#define NUM_BOXES 2

const box boxes[] = box[2](
  /* The ground */
  box(vec3(-5.0, -0.1, -5.0), vec3(5.0, 0.0, 5.0)),
  /* Box in the middle */
  box(vec3(-0.5, 0.0, -0.5), vec3(0.5, 1.0, 0.5))
);

struct hitinfo {
  vec2 lambda;
  int bi;
};

vec2 intersectBox(vec3 origin, vec3 dir, const box b) {
  vec3 tMin = (b.min - origin) / dir;
  vec3 tMax = (b.max - origin) / dir;
  vec3 t1 = min(tMin, tMax);
  vec3 t2 = max(tMin, tMax);
  float tNear = max(max(t1.x, t1.y), t1.z);
  float tFar = min(min(t2.x, t2.y), t2.z);
  return vec2(tNear, tFar);
}

bool intersectBoxes(vec3 origin, vec3 dir, out hitinfo info) {
  float smallest = MAX_SCENE_BOUNDS;
  bool found = false;
  for (int i = 0; i < NUM_BOXES; i++) {
    vec2 lambda = intersectBox(origin, dir, boxes[i]);
    if (lambda.x > 0.0 && lambda.x < lambda.y && lambda.x < smallest) {
      info.lambda = lambda;
      info.bi = i;
      smallest = lambda.x;
      found = true;
    }
  }
  return found;
}

vec4 trace(vec3 origin, vec3 dir) {
  hitinfo i;
  if (intersectBoxes(origin, dir, i)) {
    vec4 gray = vec4(i.bi / 10.0 + 0.8);
    return vec4(gray.rg, 1.0, 1.0);
  }
  return vec4(0.0, 1.0, 0.0, 1.0);
}

void main(void) {
    vec2 pos = gl_FragCoord.xy / u_screenRes;
//  vec2 pos = vsOut.Position.xy;
    vec3 dir = mix(mix(ray00, ray01, pos.y), mix(ray10, ray11, pos.y), pos.x);
  Colour = trace(eye, dir);
//    Colour = vec4(pos, 0.0, 1.0);
}
